import Trie "mo:base/Trie";
import Hash "mo:base/Hash";
import Nat "mo:base/Nat";
import Text "mo:base/Text";
import Principal "mo:base/Principal";
import Result "mo:base/Result";
import Iter "mo:base/Iter";
import Option "mo:base/Option";
import Types "./Types";
import Migration "./Migration";
import Buffer "mo:base/Buffer";
import Time "mo:base/Time";
// WARNING REMOVE? unused field "recurringTimer"
import { setTimer; recurringTimer } = "mo:base/Timer";
import Int "mo:base/Int";
import Debug "mo:base/Debug";
import Array "mo:base/Array";

persistent actor {
  type TimerId = Nat;
  // Reject AnonymousIdentity
  var anonIdNew : Text = "2vxsx-fae";
  var profiles : Types.Profile = Trie.empty();
  var childNumber : Nat = 1;
  //for keeping the child to tasks mapping
  var childToTasks : Types.TaskMap = Trie.empty();
  var childToTaskNumber : Trie.Trie<Text, Nat> = Trie.empty();
  //for keeping the child to transactions mapping
  var childToTransactions : Types.TransactionMap = Trie.empty();
  var childToTransactionNumber : Trie.Trie<Text, Nat> = Trie.empty();
  //for keeping the child to goals mapping
  var childToGoals : Types.GoalMap = Trie.empty();
  var childToGoalNumber : Trie.Trie<Text, Nat> = Trie.empty();
  //for setting up child's current goal
  var childToCurrentGoal : Trie.Trie<Text, Nat> = Trie.empty();

  // MILESTONE #1
  //for mapping child's doocoins balance to child
  var childToBalance : Trie.Trie<Text, Nat> = Trie.empty();
  //for magicCode child app onboarding OTP
  var childPins : Trie.Trie<Text, Nat> = Trie.empty();
  var childIdsFromPin : Trie.Trie<Nat, Text> = Trie.empty();
  // MILESTONE #2
  //for child to request task complete and request claim reward
  var childRequestsTasks : Trie.Trie<Text, Types.TaskReqMap> = Trie.empty();
  var childRequestsRewards : Trie.Trie<Text, Types.RewardReqMap> = Trie.empty();

  // MIGRATION: NFID to Internet Identity principal linking
  var principalLinks : Migration.PrincipalLinks = Migration.initLinks();

  // AUTH BROKER: Ephemeral storage for one-time code exchanges
  // Stores a short-lived blob (e.g., serialized delegation package) keyed by a random code
  // and protected by a caller-provided nonce. Entries are deleted on successful read.
  // expiresAt uses Time.now() units (nanoseconds since Unix epoch).
  var authBlobs : Trie.Trie<Text, { nonce : Text; blob : Blob; expiresAt : Int }> = Trie.empty();

  //who am I
  //----------------------------------------------------------------------------------------------------

  public shared query (msg) func whoami() : async Principal {
    msg.caller;
  };

  // MILESTONE #1 METHODS
  // This section contains functions related to managing one-time passwords (OTPs)
  // used for onboarding child apps within the magicCode system.
  //----------------------------------------------------------------------------------------------------

  // burnCode function initiates the burning process for a provided OTP (pin).
  // Burning essentially removes the pin from the system after a specific time.
  // public shared (msg) func burnCode<system>(pin : Nat) : async Nat {
  public shared func burnCode<system>(pin : Nat) : async Nat {
    Debug.print "in burn function!";
    let now = Time.now();
    let oneMinute = 1_000_000_000 * 60 * 1;
    // Find the child app ID associated with the provided pin using a Trie data structure
    let childId = Trie.find(
      childIdsFromPin,
      keyNat(pin),
      Nat.equal,
    );
    Debug.print "before setting timers after childID!";
    // Define an async function to perform the actual burning process
    func burnCodeAsync() : async () {
      Debug.print "Starting the burn function!";
      // Print debug information about the pin and child ID (Often used with `debug_show` to convert values to Text)
      Debug.print(debug_show (pin) # "     " #debug_show (childId));
      // Remove the pin and child ID association from their respective Trie structures
      // WARNING REMOVE? unused identifier "oldPin"
      let (newChildPins, oldPin) = Trie.remove(childPins, keyText(nullToText(childId)), Text.equal);
      // WARNING REMOVE? unused identifier "oldIds"
      let (newChildIdsFromPin, oldIds) = Trie.remove(childIdsFromPin, keyNat(pin), Nat.equal);
      // Update the in-memory state of the Trie structures
      childPins := newChildPins;
      childIdsFromPin := newChildIdsFromPin;
    };
    // Schedule a timer to call the burnCodeAsync function after the timeout duration
    Debug.print("Setting timers!" #debug_show (Int.abs(now - oneMinute)));
    ignore setTimer<system>(
      #seconds(60 * 60), // 60 mins
      func() : async () {

        await burnCodeAsync();
      },
    );
    // Return the provided pin (possibly for reference)
    return pin;
  };
  // _randomPin function generates a random 4-digit integer to be used as an OTP.
  func _randomPin() : async Nat {
    let now = Time.now();
    let seed = Int.abs(now) % 8889 + 1111; // Simple 4-digit random number between 1111-9999
    seed;
  };
  // checkMagiCode function checks if a provided OTP (pin) exists in the system and returns the associated child app ID if found.
  // public shared (msg) func checkMagiCode(pin : Nat) : async ?Text {
  public shared func checkMagiCode(pin : Nat) : async ?Text {
    Debug.print("checking at the magic code");
    // Find the child app ID associated with the provided pin using a Trie data structure
    let childId = Trie.find(
      childIdsFromPin,
      keyNat(pin),
      Nat.equal,
    );
    // If the pin is found, return the child ID
    if (childId != null) {
      return childId;
    };
    // If the pin is not found, return null
    return ?"";
  };

  // magicCode function creates a new OTP (pin) and associates it with a provided child app ID.
  // It also initiates the burning process for the pin after a specific time.
  // public shared (msg) func magicCode(childId : Text) : async ?Nat {
  public shared func magicCode(childId : Text) : async ?Nat {
    // Check if a pin already exists for the provided child ID
    let pinExists = Trie.find(
      childPins,
      keyText(childId),
      Text.equal,
    );
    // If a pin already exists, return it (no need to create a new one)
    if (pinExists != null) {
      return pinExists;
    };
    // If no pin exists, generate a new random 4-digit integer
    let pin : Nat = await _randomPin();
    // Add the association between the child ID and the new pin to the childPins Trie
    // WARNING REMOVE? unused identifier "oldPins"
    let (newChildPins, oldPins) = Trie.put(
      childPins,
      keyText(childId),
      Text.equal,
      pin,
    );
    childPins := newChildPins;
    // Retrieve the newly added pin from the childPins Trie for verification
    let childPinStore = Trie.find(
      childPins,
      keyText(childId),
      Text.equal,
    );
    // Add the association between the new pin and the child ID to the childIdsFromPin Trie
    // WARNING REMOVE? unused identifier "childPinToOld"
    let (childPinToId, childPinToOld) = Trie.put(
      childIdsFromPin,
      keyNat(pin),
      Nat.equal,
      childId,
    );
    // Update the childIdsFromPin Trie
    childIdsFromPin := childPinToId;
    // Initiate the burning process for the newly created pin with a timeout
    // WARNING REMOVE? unused identifier "burnt"
    let burnt = await burnCode(nullToNat(childPinStore));
    // Return the newly created pin
    return childPinStore;
  };

  // END MILESTONE #1 TASKS

  // AUTH BROKER ENDPOINTS
  //-----------------------------------------------------------------------------------------------
  // putAuthBlob: Store a one-time blob under a random code with a nonce and expiry (TTL).
  public shared func putAuthBlob(code : Text, nonce : Text, blob : Blob, expiresAt : Int) : async Bool {
    // Reject obviously short codes to avoid accidental collisions
    if (Text.size(code) < 16) { return false };
    // Reject expired entries
    if (expiresAt <= Time.now()) { return false };
    let (updated, _old) = Trie.put(
      authBlobs,
      keyText(code),
      Text.equal,
      {
        nonce;
        blob;
        expiresAt;
      },
    );
    authBlobs := updated;
    return true;
  };

  // takeAuthBlob: Return and delete the blob if the code+nonce match and TTL is valid.
  public shared func takeAuthBlob(code : Text, nonce : Text) : async ?Blob {
    let entryOpt = Trie.find(authBlobs, keyText(code), Text.equal);
    switch (entryOpt) {
      case null { return null };
      case (?entry) {
        // Enforce nonce match and TTL
        if (entry.nonce != nonce) {
          return null;
        };
        if (entry.expiresAt <= Time.now()) {
          // Expired: delete and return null
          let (next, _old) = Trie.remove(authBlobs, keyText(code), Text.equal);
          authBlobs := next;
          return null;
        };
        // Valid: delete-on-read and return blob
        let (next, _old) = Trie.remove(authBlobs, keyText(code), Text.equal);
        authBlobs := next;
        return ?entry.blob;
      };
    };
  };

  //count users
  //----------------------------------------------------------------------------------------------------
  public shared query func numberOfProfiles() : async Nat {
    return childNumber;
  };

  //creating a new child record
  //----------------------------------------------------------------------------------------------------
  public shared (msg) func addChild(child : Types.ChildCall) : async Result.Result<Types.Child, Types.Error> {
    let callerId = msg.caller;

    if (Principal.toText(callerId) == anonIdNew) {
      return #err(#NotAuthorized);
    };

    // Resolve principal: use NFID principal if this II principal is linked
    let resolvedPrincipal = Migration.resolvePrincipal(principalLinks, callerId);
    let childId = Principal.toText(resolvedPrincipal) # "-" # Nat.toText(childNumber);
    childNumber += 1;
    let finalChild : Types.Child = {
      name = child.name;
      id = childId;
      archived = false;
    };

    //Initializing task number to this child
    // WARNING REMOVE? unused identifier "existingTask"
    let (newChildToTaskNumber, existingTask) = Trie.put(
      childToTaskNumber,
      keyText(childId),
      Text.equal,
      1,
    );
    childToTaskNumber := newChildToTaskNumber;

    // WARNING REMOVE? unused identifier "existing"
    let (childtobalancemap, existing) = Trie.put(
      childToBalance,
      keyText(childId),
      Text.equal,
      0,
    );
    childToBalance := childtobalancemap;

    //Initializing goal (reward) number to this child
    // WARNING REMOVE? unused identifier "existingGoal"
    let (newChildToGoalNumber, existingGoal) = Trie.put(
      childToGoalNumber,
      keyText(childId),
      Text.equal,
      1,
    );
    childToGoalNumber := newChildToGoalNumber;

    //Initializing transaction number to this child
    // WARNING REMOVE? unused identifier "existingTransaction"
    let (newChildToTransactionNumber, existingTransaction) = Trie.put(
      childToTransactionNumber,
      keyText(childId),
      Text.equal,
      1,
    );
    childToTransactionNumber := newChildToTransactionNumber;

    let newProfiles = Trie.put2D(
      profiles,
      keyPrincipal(resolvedPrincipal),
      Principal.equal,
      keyText(childId),
      Text.equal,
      finalChild,
    );
    profiles := newProfiles;
    return #ok(finalChild);
  };

  //Add a task
  //Parametes needed: childId and Task (name and value)
  //----------------------------------------------------------------------------------------------------
  public shared (msg) func addTask(task : Types.TaskCall, childId : Text) : async Result.Result<[Types.Task], Types.Error> {
    let callerId = msg.caller;

    if (Principal.toText(callerId) == anonIdNew) {
      return #err(#NotAuthorized);
    };

    //Getting pointer of current task number of the child
    let currentTaskNumberPointer = Trie.find(
      childToTaskNumber,
      keyText(childId),
      Text.equal,
    );

    let finalPointer : Nat = if (task.id >= 0) { task.id } else {
      Option.get(currentTaskNumberPointer, 0);
    };
    let taskFinal : Types.Task = {
      name = task.name;
      value = task.value;
      id = finalPointer;
      archived = false;
    };
    switch (finalPointer) {
      case 0 {
        #err(#NotFound);
      };
      case (v) {
        // WARNING REMOVE? unused identifier "existing"
        let (newMap, existing) = Trie.put(
          childToTaskNumber,
          keyText(childId),
          Text.equal,
          finalPointer + 1,
        );

        childToTaskNumber := newMap;

        let newChildToTasks = Trie.put2D(
          childToTasks,
          keyText(childId),
          Text.equal,
          keyNat(finalPointer),
          Nat.equal,
          taskFinal,
        );

        childToTasks := newChildToTasks;

        let myChildTasks = Trie.find(
          childToTasks,
          keyText(childId),
          Text.equal,
        );
        let myChildTasksFormatted = Option.get(myChildTasks, Trie.empty());
        return #ok(Trie.toArray(myChildTasksFormatted, extractTasks));
      };
    };
  };

  // Get all the children
  //----------------------------------------------------------------------------------------------------

  public shared (msg) func getChildren() : async Result.Result<[Types.Child], Types.Error> {
    let callerId = msg.caller;
    let unArchivedChilds : Buffer.Buffer<Types.Child> = Buffer.Buffer<Types.Child>(0);

    if (Principal.toText(callerId) == anonIdNew) {
      return #err(#NotAuthorized);
    };

    // Resolve principal: use NFID principal if this II principal is linked
    let resolvedPrincipal = Migration.resolvePrincipal(principalLinks, callerId);

    let allChildren = Trie.find(
      profiles,
      keyPrincipal(resolvedPrincipal),
      Principal.equal,
    );
    let allChildrenFormatted = Option.get(allChildren, Trie.empty());
    let agnosticArchivedChildList = Trie.toArray(allChildrenFormatted, extractChildren);

    for (child in agnosticArchivedChildList.vals()) {
      if (child.archived == false) {
        unArchivedChilds.add(child);
      };
    };

    return #ok(Buffer.toArray(unArchivedChilds));
  };

  //Get the childs tasks
  //Parametes needed: childId
  //----------------------------------------------------------------------------------------------------

  // public shared (msg) func getTasks(childId : Text) : async Result.Result<[Types.Task], Types.Error> {
  public shared func getTasks(childId : Text) : async Result.Result<[Types.Task], Types.Error> {
    Debug.print("üì¶ getTasks called for childId: " # childId);
    let myChildTasks = Trie.find(
      childToTasks,
      keyText(childId),
      Text.equal,
    );
    let myChildTasksFormatted = Option.get(myChildTasks, Trie.empty());
    let agnosticArchivedChildTaskList = Trie.toArray(myChildTasksFormatted, extractTasks);

    Debug.print("üì¶ Total tasks found: " # Nat.toText(agnosticArchivedChildTaskList.size()));

    // AUTO-CLEANUP: Build new trie with only unique, unarchived tasks
    var cleanedTrie : Trie.Trie<Nat, Types.Task> = Trie.empty();
    var seenIds : Buffer.Buffer<Nat> = Buffer.Buffer<Nat>(0);
    let unArchivedChildsTasks : Buffer.Buffer<Types.Task> = Buffer.Buffer<Types.Task>(0);
    var duplicatesRemoved : Nat = 0;
    var archivedRemoved : Nat = 0;

    for (task in agnosticArchivedChildTaskList.vals()) {
      Debug.print("üì¶ Task ID=" # Nat.toText(task.id) # " name=" # task.name # " archived=" # debug_show (task.archived));

      let taskId = task.id;
      let alreadySeen = Array.find<Nat>(Buffer.toArray(seenIds), func(id) = id == taskId);

      // Reconstruct task to ensure archived field exists
      let migratedTask : Types.Task = {
        name = task.name;
        value = task.value;
        id = task.id;
        archived = task.archived;
      };

      if (alreadySeen == null and migratedTask.archived == false) {
        // First occurrence and not archived - keep it
        let (newTrie, _old) = Trie.put(
          cleanedTrie,
          keyNat(taskId),
          Nat.equal,
          migratedTask,
        );
        cleanedTrie := newTrie;
        seenIds.add(taskId);
        unArchivedChildsTasks.add(migratedTask);
      } else if (alreadySeen != null) {
        duplicatesRemoved += 1;
        Debug.print("üóëÔ∏è Auto-removing DUPLICATE task ID=" # Nat.toText(taskId));
      } else if (migratedTask.archived == true) {
        archivedRemoved += 1;
        Debug.print("üóëÔ∏è Auto-removing ARCHIVED task ID=" # Nat.toText(taskId));
      };
    };

    if (duplicatesRemoved > 0 or archivedRemoved > 0) {
      Debug.print("üßπ AUTO-CLEANUP: Removed " # Nat.toText(duplicatesRemoved) # " duplicate tasks and " # Nat.toText(archivedRemoved) # " archived tasks");
      // Save cleaned trie back to storage
      let (updatedChildToTasks, _) = Trie.put(
        childToTasks,
        keyText(childId),
        Text.equal,
        cleanedTrie,
      );
      childToTasks := updatedChildToTasks;
    };

    Debug.print("üì¶ Returning " # Nat.toText(unArchivedChildsTasks.size()) # " unarchived tasks");
    return #ok(Buffer.toArray(unArchivedChildsTasks));
  };

  //Add goal
  //Parametes needed: childId and Goal
  //----------------------------------------------------------------------------------------------------

  public shared (msg) func addGoal(goal : Types.GoalCall, childId : Text) : async Result.Result<[Types.Goal], Types.Error> {
    let callerId = msg.caller;

    if (Principal.toText(callerId) == anonIdNew) {
      return #err(#NotAuthorized);
    };

    //Getting pointer of current task number of the child
    let currentGoalNumberPointer = Trie.find(
      childToGoalNumber,
      keyText(childId),
      Text.equal,
    );

    let finalPointer : Nat = Option.get(currentGoalNumberPointer, 0);

    let finalGoalObject : Types.Goal = {
      name = goal.name;
      value = goal.value;
      id = finalPointer;
      archived = false;
    };

    switch (finalPointer) {
      case 0 {
        #err(#NotFound);
      };
      case (v) {
        // WARNING REMOVE? unused identifier "existing"
        let (newMap, existing) = Trie.put(
          childToGoalNumber,
          keyText(childId),
          Text.equal,
          finalPointer + 1,
        );

        childToGoalNumber := newMap;

        let newChildToGoals = Trie.put2D(
          childToGoals,
          keyText(childId),
          Text.equal,
          keyNat(finalPointer),
          Nat.equal,
          finalGoalObject,
        );

        childToGoals := newChildToGoals;
        let myChildGoals = Trie.find(
          childToGoals,
          keyText(childId),
          Text.equal,
        );
        let myChildGoalsFormatted = Option.get(myChildGoals, Trie.empty());
        let allGoals = Trie.toArray(myChildGoalsFormatted, extractGoals);

        // Filter out archived goals before returning
        let unArchivedGoals : Buffer.Buffer<Types.Goal> = Buffer.Buffer<Types.Goal>(0);
        for (goal in allGoals.vals()) {
          if (goal.archived == false) {
            unArchivedGoals.add(goal);
          };
        };
        return #ok(Buffer.toArray(unArchivedGoals));
      };
    };
  };

  //Set the childs current goal
  //Parametes needed: childId and goalId
  //----------------------------------------------------------------------------------------------------

  // public shared (msg) func currentGoal(childId : Text, goalId : Nat) : async Result.Result<(), Types.Error> {
  public shared func currentGoal(childId : Text, goalId : Nat) : async Result.Result<(), Types.Error> {
    // WARNING REMOVE? unused identifier "existing"
    let (updateChildToGoalNumber, existing) = Trie.put(
      childToCurrentGoal,
      keyText(childId),
      Text.equal,
      goalId,
    );
    childToCurrentGoal := updateChildToGoalNumber;
    return #ok(());
  };

  //Get childs transactions
  //
  //----------------------------------------------------------------------------------------------------

  public func getTransactions(childId : Text) : async Result.Result<[Types.Transaction], Types.Error> {
    let myChildTransactions = Trie.find(
      childToTransactions,
      keyText(childId),
      Text.equal,
    );
    let myChildTransactionsFormatted = Option.get(myChildTransactions, Trie.empty());
    return #ok(Trie.toArray(myChildTransactionsFormatted, extractTransactions));
  };

  //Get childs goals (rewards)
  //
  //----------------------------------------------------------------------------------------------------

  public func getGoals(childId : Text) : async Result.Result<[Types.Goal], Types.Error> {
    Debug.print("üì¶ getGoals called for childId: " # childId);
    let myChildGoals = Trie.find(
      childToGoals,
      keyText(childId),
      Text.equal,
    );
    let myChildGoalsFormatted = Option.get(myChildGoals, Trie.empty());
    let agnosticArchivedGoalList = Trie.toArray(myChildGoalsFormatted, extractGoals);

    Debug.print("üì¶ Total goals found: " # Nat.toText(agnosticArchivedGoalList.size()));

    // AUTO-CLEANUP: Build new trie with only unique, unarchived goals
    var cleanedTrie : Trie.Trie<Nat, Types.Goal> = Trie.empty();
    var seenIds : Buffer.Buffer<Nat> = Buffer.Buffer<Nat>(0);
    let unArchivedGoals : Buffer.Buffer<Types.Goal> = Buffer.Buffer<Types.Goal>(0);
    var duplicatesRemoved : Nat = 0;
    var archivedRemoved : Nat = 0;

    for (goal in agnosticArchivedGoalList.vals()) {
      Debug.print("üì¶ Goal ID=" # Nat.toText(goal.id) # " name=" # goal.name # " archived=" # debug_show (goal.archived));

      let goalId = goal.id;
      let alreadySeen = Array.find<Nat>(Buffer.toArray(seenIds), func(id) = id == goalId);

      // Reconstruct goal to ensure archived field exists
      let migratedGoal : Types.Goal = {
        name = goal.name;
        value = goal.value;
        id = goal.id;
        archived = goal.archived;
      };

      if (alreadySeen == null and migratedGoal.archived == false) {
        // First occurrence and not archived - keep it
        let (newTrie, _old) = Trie.put(
          cleanedTrie,
          keyNat(goalId),
          Nat.equal,
          migratedGoal,
        );
        cleanedTrie := newTrie;
        seenIds.add(goalId);
        unArchivedGoals.add(migratedGoal);
      } else if (alreadySeen != null) {
        duplicatesRemoved += 1;
        Debug.print("üóëÔ∏è Auto-removing DUPLICATE goal ID=" # Nat.toText(goalId));
      } else if (migratedGoal.archived == true) {
        archivedRemoved += 1;
        Debug.print("üóëÔ∏è Auto-removing ARCHIVED goal ID=" # Nat.toText(goalId));
      };
    };

    if (duplicatesRemoved > 0 or archivedRemoved > 0) {
      Debug.print("üßπ AUTO-CLEANUP: Removed " # Nat.toText(duplicatesRemoved) # " duplicates and " # Nat.toText(archivedRemoved) # " archived goals");
      // Save cleaned trie back to storage
      let (updatedChildToGoals, _) = Trie.put(
        childToGoals,
        keyText(childId),
        Text.equal,
        cleanedTrie,
      );
      childToGoals := updatedChildToGoals;
    };

    Debug.print("üì¶ Returning " # Nat.toText(unArchivedGoals.size()) # " unarchived goals");
    return #ok(Buffer.toArray(unArchivedGoals));
  };

  //Approve a childs task
  //Parametes needed: childId and taskId
  //----------------------------------------------------------------------------------------------------

  public shared (msg) func approveTask(childId : Text, taskId : Nat, completedDate : Text) : async Result.Result<(), Types.Error> {
    let callerId = msg.caller;

    if (Principal.toText(callerId) == anonIdNew) {
      return #err(#NotAuthorized);
    };

    let myChildTasks = Trie.find(
      childToTasks,
      keyText(childId),
      Text.equal,
    );

    let myChildTasksFormatted : Trie.Trie<Nat, Types.Task> = Option.get(myChildTasks, Trie.empty());

    let targetTask = Trie.find(
      myChildTasksFormatted,
      keyNat(taskId),
      Nat.equal,
    );
    switch (targetTask) {
      case null {
        #err(#NotFound);
      };
      case (?v) {
        let value : Nat = v.value;
        // WARNING REMOVE? unused identifier "allTransactions"
        let (allTransactions, currentPointer) = returnTransactionDetails(childId);
        let transactionObject : Types.Transaction = {
          name = v.name;
          value = value;
          completedDate = completedDate;
          transactionType = "TASK_CREDIT";
          id = currentPointer;
        };
        let newChildToTransactionMap = Trie.put2D(
          childToTransactions,
          keyText(childId),
          Text.equal,
          keyNat(currentPointer),
          Nat.equal,
          transactionObject,
        );
        childToTransactions := newChildToTransactionMap;
        let myBalance = await getBalance(childId);
        let currentBalanceFormatted = Nat.add(myBalance, value);
        // WARNING REMOVE? unused identifier "existing"
        let (updatedBalanceMap, existing) = Trie.put(
          childToBalance,
          keyText(childId),
          Text.equal,
          currentBalanceFormatted,
        );
        childToBalance := updatedBalanceMap;
        #ok(());
      };
    };

  };

  //Claim childs goal (reward)
  //Parametes needed: childId and goalId
  //----------------------------------------------------------------------------------------------------
  public shared (msg) func claimGoal(childId : Text, goalId : Nat, completedDate : Text) : async Result.Result<(), Types.Error> {
    let callerId = msg.caller;

    if (Principal.toText(callerId) == anonIdNew) {
      return #err(#NotAuthorized);
    };

    let myGoals : ?Trie.Trie<Nat, Types.Goal> = Trie.find(
      childToGoals,
      keyText(childId),
      Text.equal,
    );

    let myChildGoalsFormatted : Trie.Trie<Nat, Types.Goal> = Option.get(myGoals, Trie.empty());

    let targetGoal = Trie.find(
      myChildGoalsFormatted,
      keyNat(goalId),
      Nat.equal,
    );
    switch (targetGoal) {
      case null {
        #err(#NotFound);
      };
      case (?v) {
        let value : Nat = v.value;
        let myBalance = await getBalance(childId);
        if (value > myBalance) {
          return #err(#BalanceNotEnough);
        };
        // WARNING REMOVE? unused identifier "allTransactions"
        let (allTransactions, currentPointer) = returnTransactionDetails(childId);
        let transactionObject : Types.Transaction = {
          name = v.name;
          value = value;
          completedDate = completedDate;
          transactionType = "GOAL_DEBIT";
          id = currentPointer;
        };

        let newChildToTransactionMap = Trie.put2D(
          childToTransactions,
          keyText(childId),
          Text.equal,
          keyNat(currentPointer),
          Nat.equal,
          transactionObject,
        );
        childToTransactions := newChildToTransactionMap;

        let currentBalanceFormatted = Nat.sub(myBalance, value);
        // WARNING REMOVE? unused identifier "existing"
        let (updatedBalanceMap, existing) = Trie.put(
          childToBalance,
          keyText(childId),
          Text.equal,
          currentBalanceFormatted,
        );
        childToBalance := updatedBalanceMap;
        #ok(());
      };
    };
  };

  //Get childs current goal
  //Parametes needed: childId
  //----------------------------------------------------------------------------------------------------
  public func getCurrentGoal(childId : Text) : async Nat {
    let currentGoalNumber = Trie.find(
      childToCurrentGoal,
      keyText(childId),
      Text.equal,
    );
    let currentGoalNumberFormatted = Option.get(currentGoalNumber, 0);
    return currentGoalNumberFormatted;
  };

  //Update childs task
  //Parametes needed: childId, taskNumber and updated task object
  //----------------------------------------------------------------------------------------------------
  public shared (msg) func updateTask(childId : Text, taskNumber : Nat, updatedTask : Types.Task) : async Result.Result<(), Types.Error> {
    let callerId = msg.caller;
    if (Principal.toText(callerId) == anonIdNew) {
      return #err(#NotAuthorized);
    };

    let updatedChildToTasks = Trie.put2D(
      childToTasks,
      keyText(childId),
      Text.equal,
      keyNat(taskNumber),
      Nat.equal,
      updatedTask,
    );
    childToTasks := updatedChildToTasks;
    return #ok(());
  };

  //Update child
  //Parametes needed: childId and updated child object.
  //----------------------------------------------------------------------------------------------------
  public shared (msg) func updateChild(childId : Text, child : Types.Child) : async Result.Result<(), Types.Error> {
    let callerId = msg.caller;
    if (Principal.toText(callerId) == anonIdNew) {
      return #err(#NotAuthorized);
    };

    // Resolve principal: use NFID principal if this II principal is linked
    let resolvedPrincipal = Migration.resolvePrincipal(principalLinks, callerId);

    let profilesUpdate = Trie.put2D(
      profiles,
      keyPrincipal(resolvedPrincipal),
      Principal.equal,
      keyText(childId),
      Text.equal,
      child,
    );
    profiles := profilesUpdate;
    return #ok(());
  };

  public shared (msg) func updateGoal(childId : Text, goalId : Nat, updatedGoal : Types.Goal) : async Result.Result<(), Types.Error> {
    let callerId = msg.caller;
    if (Principal.toText(callerId) == anonIdNew) {
      return #err(#NotAuthorized);
    };

    // Debug logging
    Debug.print("üîß updateGoal called for goalId: " # Nat.toText(goalId));
    Debug.print("üîß updatedGoal.archived = " # debug_show (updatedGoal.archived));
    Debug.print("üîß updatedGoal.name = " # updatedGoal.name);

    // FORCE UPDATE: Retrieve existing goal and rebuild it to ensure archived field persists
    let existingGoals = Trie.find(childToGoals, keyText(childId), Text.equal);
    let existingGoalsFormatted = Option.get(existingGoals, Trie.empty());
    let existingGoal = Trie.find(existingGoalsFormatted, keyNat(goalId), Nat.equal);

    // Create new goal with explicit archived field
    let finalUpdatedGoal : Types.Goal = {
      name = updatedGoal.name;
      value = updatedGoal.value;
      id = updatedGoal.id;
      archived = updatedGoal.archived;
    };

    Debug.print("üîß finalUpdatedGoal.archived = " # debug_show (finalUpdatedGoal.archived));

    let updatedChildToGoals = Trie.put2D(
      childToGoals,
      keyText(childId),
      Text.equal,
      keyNat(goalId),
      Nat.equal,
      finalUpdatedGoal,
    );
    childToGoals := updatedChildToGoals;

    // Verify the update
    let verifyGoals = Trie.find(childToGoals, keyText(childId), Text.equal);
    let verifyGoalsFormatted = Option.get(verifyGoals, Trie.empty());
    let verifiedGoal = Trie.find(verifyGoalsFormatted, keyNat(goalId), Nat.equal);
    switch (verifiedGoal) {
      case null { Debug.print("‚ùå Goal not found after update!") };
      case (?g) {
        Debug.print("‚úÖ Verified goal.archived = " # debug_show (g.archived));
      };
    };

    return #ok(());
  };

  private func keyPrincipal(x : Principal) : Trie.Key<Principal> {
    return { key = x; hash = Principal.hash(x) };
  };

  private func keyText(x : Text) : Trie.Key<Text> {
    return { key = x; hash = Text.hash(x) };
  };
  // WARNING REMOVE? unused identifier "keyTextNull"
  private func keyTextNull(x : Text) : Trie.Key<Text> {
    return { key = x; hash = Text.hash(x) };
  };

  private func keyNat(x : Nat) : Trie.Key<Nat> {
    return { key = x; hash = Nat.toText(x) |> Text.hash(_) };
  };

  // WARNING REMOVE? unused identifier "k"
  private func extractChildren(k : Text, v : Types.Child) : Types.Child {
    return v;
  };

  // WARNING REMOVE? unused identifier "k"
  private func extractTasks(k : Nat, v : Types.Task) : Types.Task {
    return v;
  };
  // WARNING REMOVE? unused identifier "k"
  private func extractTasksReq(k : Text, v : Types.TaskRequest) : Types.TaskRequest {
    return v;
  };
  // WARNING REMOVE? unused identifier "k"
  private func extractReReq(k : Text, v : Types.RewardRequest) : Types.RewardRequest {
    return v;
  };
  // WARNING REMOVE? unused identifier "k"
  private func extractTransactions(k : Nat, v : Types.Transaction) : Types.Transaction {
    return v;
  };
  // WARNING REMOVE? unused identifier "k"
  private func extractGoals(k : Nat, v : Types.Goal) : Types.Goal {
    return v;
  };

  private func nullToText(msg : ?Text) : Text {
    switch (msg) {
      case (?string) {
        return string;
      };
      case (null) {
        return "";
      };
    };
  };

  private func nullToNat(msg : ?Nat) : Nat {
    switch (msg) {
      case (?string) {
        return string;
      };
      case (null) {
        return 0;
      };
    };
  };

  private func returnTransactionDetails(childId : Text) : (Trie.Trie<Nat, Types.Transaction>, Nat) {
    let myTransactions : ?Trie.Trie<Nat, Types.Transaction> = Trie.find(
      childToTransactions,
      keyText(childId),
      Text.equal,
    );

    let myTransactionsFormatted : Trie.Trie<Nat, Types.Transaction> = Option.get(myTransactions, Trie.empty());
    var currentPointer : Nat = Trie.size(myTransactionsFormatted);
    currentPointer += 1;
    return (myTransactionsFormatted, currentPointer);
  };

  public func getBalance(childId : Text) : async Nat {
    let currentBalance = Trie.find(
      childToBalance,
      keyText(childId),
      Text.equal,
    );
    let currentBalanceFormatted = Option.get(currentBalance, 0);
    return currentBalanceFormatted;
  };

  // MILESTONE #2
  //Request rewards and task complete

  // This section contains functions related to managing task and reward requests
  // submitted by child apps.

  // requestTaskComplete function allows a child app to submit a request indicating that a task has been completed.
  // It creates a new "TaskRequest" object with details about the child, task, and reward value.
  // The request ID is a combination of child ID, task ID, and a random pin.
  // The function then stores the request in a Trie data structure identified by the child ID.
  // It returns the request ID as a reference.

  // public shared (msg) func requestTaskComplete(childId : Text, taskId : Nat, name : Text, value : Nat) : async Text {
  public shared func requestTaskComplete(childId : Text, taskId : Nat, name : Text, value : Nat) : async Text {
    // Generate a random 4-digit pin
    let randomPin = await _randomPin();
    // Construct the request ID by combining child ID, task ID, and random pin
    let requestId = childId # "-" #Nat.toText(taskId) #Nat.toText(randomPin);
    // Create a new "TaskRequest" object with details about the request
    let task : Types.TaskRequest = {
      childId;
      taskId;
      id = requestId;
      name = name;
      value = value;
    };
    // Find the existing task requests for the child (initially empty Trie)
    let allChildTasks = Trie.find(
      childRequestsTasks,
      keyText(childId),
      Text.equal,
    );
    // Get the existing task requests as an Option type (may be null)
    let allChildrenTaskormatted = Option.get(allChildTasks, Trie.empty());
    // Add the new task request to the child's existing requests (or create a new map if none exist)
    // WARNING REMOVE? unused identifier "oldLV2"
    let (allChildTasksLV2, oldLV2) = Trie.put(
      allChildrenTaskormatted,
      keyText(requestId),
      Text.equal,
      task,
    );
    // Update the child's task requests in the main Trie data structure
    // WARNING REMOVE? unused identifier "oldLV1"
    let (allChildTasksUpdate, oldLV1) = Trie.put(
      childRequestsTasks,
      keyText(childId),
      Text.equal,
      allChildTasksLV2,
    );
    // Update the in-memory state of the childRequestsTasks Trie
    childRequestsTasks := allChildTasksUpdate;
    // Return the generated request ID
    return requestId;

  };

  // requestClaimReward function is similar to requestTaskComplete, but for claiming rewards.
  // It creates a "RewardRequest" object and stores it in the childRequestsRewards Trie.
  // public shared (msg) func requestClaimReward(childId : Text, rewardId : Nat, value : Nat, name : Text) : async Text {
  public shared func requestClaimReward(childId : Text, rewardId : Nat, value : Nat, name : Text) : async Text {
    let randomPin = await _randomPin();
    let requestId = childId # "-" #Nat.toText(rewardId) #Nat.toText(randomPin);
    let task : Types.RewardRequest = {
      childId;
      reward = rewardId;
      id = requestId;
      value = value;
      name = name;
    };

    let allChildRewards = Trie.find(
      childRequestsRewards,
      keyText(childId),
      Text.equal,
    );

    let allChildrenRewardsFormatted = Option.get(allChildRewards, Trie.empty());
    // WARNING REMOVE? unused identifier "oldLV2"
    let (allChildRewardssLV2, oldLV2) = Trie.put(
      allChildrenRewardsFormatted,
      keyText(requestId),
      Text.equal,
      task,
    );
    // WARNING REMOVE? unused identifier "oldLV1"
    let (allChildRewardsUpdate, oldLV1) = Trie.put(
      childRequestsRewards,
      keyText(childId),
      Text.equal,
      allChildRewardssLV2,
    );

    childRequestsRewards := allChildRewardsUpdate;

    return requestId;

  };

  // getRewardReqs function retrieves all reward requests associated with a given child ID.
  // It uses Trie operations to find the child's requests, converts them to a list of "RewardRequest" objects,
  // and returns the list.
  // public shared (msg) func getRewardReqs(childId : Text) : async [Types.RewardRequest] {
  public shared func getRewardReqs(childId : Text) : async [Types.RewardRequest] {
    // Create an empty buffer to store the reward requests
    let rewardsRequestBuffer : Buffer.Buffer<Types.RewardRequest> = Buffer.Buffer<Types.RewardRequest>(0);
    // Buffer to track seen IDs for deduplication
    let seenIds : Buffer.Buffer<Text> = Buffer.Buffer<Text>(0);

    // Find the child's reward requests in the Trie
    let allChildRewards = Trie.find(
      childRequestsRewards,
      keyText(childId),
      Text.equal,
    );
    // Get the child's requests as an Option type (may be null)
    let allChildrenRewardsFormatted = Option.get(allChildRewards, Trie.empty());
    // Convert the child's requests (potentially empty Trie) to a list of "RewardRequest" objects
    let agnosticArchivedRewardslist = Trie.toArray(allChildrenRewardsFormatted, extractReReq);

    // Iterate through the list of requests and add them to the buffer (with deduplication)
    for (reward in agnosticArchivedRewardslist.vals()) {
      // Check if we've already seen this ID
      var isDuplicate = false;
      for (seenId in seenIds.vals()) {
        if (seenId == reward.id) {
          isDuplicate := true;
        };
      };

      // Only add if not a duplicate
      if (not isDuplicate) {
        rewardsRequestBuffer.add(reward);
        seenIds.add(reward.id);
      };
    };

    // If we found duplicates, clean up the trie
    if (seenIds.size() < agnosticArchivedRewardslist.size()) {
      Debug.print("üßπ Cleaning up duplicate reward requests for child: " # childId);
      // Rebuild the trie with only unique requests
      var cleanedTrie : Types.RewardReqMap = Trie.empty();
      for (reward in rewardsRequestBuffer.vals()) {
        let (newTrie, _) = Trie.put(
          cleanedTrie,
          keyText(reward.id),
          Text.equal,
          reward,
        );
        cleanedTrie := newTrie;
      };

      // Update the main trie
      let (updatedChildRequests, _) = Trie.put(
        childRequestsRewards,
        keyText(childId),
        Text.equal,
        cleanedTrie,
      );
      childRequestsRewards := updatedChildRequests;
    };

    // Convert the buffer containing requests to a Motoko array and return it
    return Buffer.toArray(rewardsRequestBuffer);
  };

  // hasRewards function checks if a child has any reward requests.
  // It follows a similar approach to getRewardReqs, but instead of returning the list,
  // it returns the number of requests found (buffer size).
  //  public shared (msg) func hasRewards(childId : Text) : async Nat {
  public shared func hasRewards(childId : Text) : async Nat {
    let rewardsRequestBuffer : Buffer.Buffer<Types.RewardRequest> = Buffer.Buffer<Types.RewardRequest>(0);

    let allChildRewards = Trie.find(
      childRequestsRewards,
      keyText(childId),
      Text.equal,
    );

    let allChildrenRewardsFormatted = Option.get(allChildRewards, Trie.empty());

    let agnosticArchivedRewardslist = Trie.toArray(allChildrenRewardsFormatted, extractReReq);

    for (reward in agnosticArchivedRewardslist.vals()) {
      rewardsRequestBuffer.add(reward);
    };

    return rewardsRequestBuffer.size();
  };

  // hasTasks function is similar to hasRewards but for task requests.
  // public shared (msg) func hasTasks(childId : Text) : async Nat {
  public shared func hasTasks(childId : Text) : async Nat {
    let tasksRequestBuffer : Buffer.Buffer<Types.TaskRequest> = Buffer.Buffer<Types.TaskRequest>(0);

    let allChildTasks = Trie.find(
      childRequestsTasks,
      keyText(childId),
      Text.equal,
    );

    let allChildrenTasksFormatted = Option.get(allChildTasks, Trie.empty());

    let agnosticArchivedTaskList = Trie.toArray(allChildrenTasksFormatted, extractTasksReq);

    for (task in agnosticArchivedTaskList.vals()) {
      tasksRequestBuffer.add(task);
    };

    return tasksRequestBuffer.size();
  };

  // getTaskReqs function retrieves all task requests associated with a given child ID.
  // It's similar to getRewardReqs but operates on task requests.
  //  public shared (msg) func getTaskReqs(childId : Text) : async [Types.TaskRequest] {
  public shared func getTaskReqs(childId : Text) : async [Types.TaskRequest] {
    let tasksRequestBuffer : Buffer.Buffer<Types.TaskRequest> = Buffer.Buffer<Types.TaskRequest>(0);
    // Buffer to track seen IDs for deduplication
    let seenIds : Buffer.Buffer<Text> = Buffer.Buffer<Text>(0);

    let allChildTasks = Trie.find(
      childRequestsTasks,
      keyText(childId),
      Text.equal,
    );

    let allChildrenTasksFormatted = Option.get(allChildTasks, Trie.empty());

    let agnosticArchivedTaskList = Trie.toArray(allChildrenTasksFormatted, extractTasksReq);

    // Iterate through the list of requests and add them to the buffer (with deduplication)
    for (task in agnosticArchivedTaskList.vals()) {
      // Check if we've already seen this ID
      var isDuplicate = false;
      for (seenId in seenIds.vals()) {
        if (seenId == task.id) {
          isDuplicate := true;
        };
      };

      // Only add if not a duplicate
      if (not isDuplicate) {
        tasksRequestBuffer.add(task);
        seenIds.add(task.id);
      };
    };

    // If we found duplicates, clean up the trie
    if (seenIds.size() < agnosticArchivedTaskList.size()) {
      Debug.print("üßπ Cleaning up duplicate task requests for child: " # childId);
      // Rebuild the trie with only unique requests
      var cleanedTrie : Types.TaskReqMap = Trie.empty();
      for (task in tasksRequestBuffer.vals()) {
        let (newTrie, _) = Trie.put(
          cleanedTrie,
          keyText(task.id),
          Text.equal,
          task,
        );
        cleanedTrie := newTrie;
      };

      // Update the main trie
      let (updatedChildRequests, _) = Trie.put(
        childRequestsTasks,
        keyText(childId),
        Text.equal,
        cleanedTrie,
      );
      childRequestsTasks := updatedChildRequests;
    };

    return Buffer.toArray(tasksRequestBuffer);
  };

  // This function allows a child app to remove a specific task request.
  // It finds the child's task requests (Trie), removes the request identified by the ID,
  // and updates the child's Trie entry with the modified list. It then returns the request ID.
  // public shared (msg) func removeTaskReq(childId : Text, id : Text) : async Text {
  public shared func removeTaskReq(childId : Text, id : Text) : async Text {
    // Find the child's task requests in the Trie
    let allChildTasks = Trie.find(
      childRequestsTasks,
      keyText(childId),
      Text.equal,
    );
    // Get the child's requests as an Option type (may be null)
    let allChildrenTaskormatted = Option.get(allChildTasks, Trie.empty());
    // Attempt to remove the task request identified by ID from the child's Trie
    // WARNING REMOVE? unused identifier "oldLV2"
    let (allChildTasksLV2, oldLV2) = Trie.remove(
      allChildrenTaskormatted,
      keyText(id),
      Text.equal,
    );
    // Update the child's task requests in the main Trie with the modified list (or empty Trie if removed)
    // WARNING REMOVE? unused identifier "oldLV1"
    let (allChildTasksUpdate, oldLV1) = Trie.put(
      childRequestsTasks,
      keyText(childId),
      Text.equal,
      allChildTasksLV2,
    );
    // Update the in-memory state of the childRequestsTasks Trie
    childRequestsTasks := allChildTasksUpdate;
    // Return the ID of the removed request
    return id;
  };

  // removeRewardReq function is similar to removeTaskReq but for reward requests.
  // public shared (msg) func removeRewardReq(childId : Text, id : Text) : async Text {
  public shared func removeRewardReq(childId : Text, id : Text) : async Text {
    let allChildRewards = Trie.find(
      childRequestsRewards,
      keyText(childId),
      Text.equal,
    );

    let allChildrenRewardsFormatted = Option.get(allChildRewards, Trie.empty());
    // WARNING REMOVE? unused identifier "oldLV2"
    let (allChildRewardssLV2, oldLV2) = Trie.remove(
      allChildrenRewardsFormatted,
      keyText(id),
      Text.equal,
    );
    // WARNING REMOVE? unused identifier "oldLV1"
    let (allChildRewardsUpdate, oldLV1) = Trie.put(
      childRequestsRewards,
      keyText(childId),
      Text.equal,
      allChildRewardssLV2,
    );

    childRequestsRewards := allChildRewardsUpdate;

    return id;
  };

  // extractCallerFromId private function extracts the principal (caller ID) from a child ID string.
  // The child ID format is assumed to be "<principal ID>-<random string>".
  // It splits the string by "-", extracts the first part, and returns it.
  private func extractCallerFromId(childId : Text) : Text {
    // Split the child ID string by "-" delimiter
    let words = Text.split(childId, #char '-');
    // Convert the split parts to a Motoko array
    let wordsArray = Iter.toArray(words);
    // Extract a slice of the array containing all elements except the last one (random string)
    let wordsSlices = Array.slice<Text>(wordsArray, 0, wordsArray.size() -1);
    // Initialize an empty string to store the extracted principal ID
    var fromIter : Text = "";
    // Iterate through the sliced array (without the random string)
    var counter = 0;
    for (word in wordsSlices) {
      // If it's the first iteration, append the word (principal ID) to the empty string
      if (counter == 0) {
        fromIter := fromIter #word;
        counter := counter + 1;
        // If it's not the first iteration, append "-" and the word (principal ID) to the string
      } else {
        fromIter := fromIter # "-" #word;
        counter := counter + 1;
      };
    };
    // Return the extracted principal ID
    return fromIter;
  };

  // getChild function retrieves the name
  // public shared (msg) func getChild(childId : Text) : async Text {
  public shared func getChild(childId : Text) : async Text {
    let fromIter = extractCallerFromId(childId);
    let callerId = Principal.fromText(nullToText(?fromIter));
    let allChildren = Trie.find(
      profiles,
      keyPrincipal(callerId),
      Principal.equal,
    );
    let allChildrenFormatted = Option.get(allChildren, Trie.empty());
    let child = Trie.find(
      allChildrenFormatted,
      keyText(childId),
      Text.equal,
    );
    switch (child) {
      case null { return "" };
      case (?e) {
        return e.name;
      };
    };
  };

  // MIGRATION FUNCTIONS: NFID to Internet Identity
  //----------------------------------------------------------------------------------------------------

  // Helper function to check if an NFID principal has existing data
  private func hasNfidData(nfidPrincipal : Principal) : Bool {
    switch (Trie.get(profiles, keyPrincipal(nfidPrincipal), Principal.equal)) {
      case null { false };
      case (?_) { true };
    };
  };

  // Link NFID principal to Internet Identity principal
  public shared (msg) func linkPrincipals(nfidPrincipal : Principal, iiPrincipal : Principal) : async Result.Result<(), Text> {
    let caller = msg.caller;

    // Create closure for data check
    let hasData = func() : Bool { hasNfidData(nfidPrincipal) };

    switch (Migration.linkPrincipals(principalLinks, caller, nfidPrincipal, iiPrincipal, hasData)) {
      case (#ok(newLinks)) {
        principalLinks := newLinks;
        #ok();
      };
      case (#err(#NotAuthorized)) {
        #err("Not authorized: Only Internet Identity principal can link");
      };
      case (#err(#NoDataFound)) { #err("No data found for NFID principal") };
      case (#err(#AlreadyLinked)) { #err("Principal already linked") };
      case (#err(#InvalidPrincipal)) { #err("Invalid principal format") };
    };
  };

  // Get migration status for current caller
  public shared query (msg) func getMigrationStatus() : async {
    nfidPrincipal : ?Text;
    isLinked : Bool;
  } {
    Migration.getMigrationStatus(principalLinks, msg.caller);
  };

  // Resolve caller principal (use NFID principal if linked, otherwise use caller)
  private func resolveCaller(caller : Principal) : Principal {
    Migration.resolvePrincipal(principalLinks, caller);
  };

  // Update existing functions to use resolveCaller
  // Example: Update getChildren to resolve principal
  public shared (msg) func getChildrenWithMigration() : async Result.Result<[Types.Child], Types.Error> {
    let resolvedCaller = resolveCaller(msg.caller);

    if (Principal.toText(resolvedCaller) == anonIdNew) {
      return #err(#NotAuthorized);
    };

    let allChildren = Trie.find(
      profiles,
      keyPrincipal(resolvedCaller),
      Principal.equal,
    );

    switch (allChildren) {
      case null {
        #err(#NotFound);
      };
      case (?e) {
        let vals = Iter.toArray(Trie.iter(e));
        let formatted = Array.map<(Text, Types.Child), Types.Child>(vals, func(val) = val.1);
        #ok(formatted);
      };
    };
  };

  // CLEANUP FUNCTION: Remove duplicates and archived goals for a child
  public shared (msg) func cleanupGoals(childId : Text) : async Result.Result<Text, Types.Error> {
    let callerId = msg.caller;
    if (Principal.toText(callerId) == anonIdNew) {
      return #err(#NotAuthorized);
    };

    let myChildGoals = Trie.find(
      childToGoals,
      keyText(childId),
      Text.equal,
    );
    let myChildGoalsFormatted = Option.get(myChildGoals, Trie.empty());
    let allGoals = Trie.toArray(myChildGoalsFormatted, extractGoals);

    Debug.print("üßπ CLEANUP: Found " # Nat.toText(allGoals.size()) # " total goals");

    // Create new trie with only unique, unarchived goals (keep first occurrence)
    var cleanedTrie : Trie.Trie<Nat, Types.Goal> = Trie.empty();
    var seenIds : Buffer.Buffer<Nat> = Buffer.Buffer<Nat>(0);
    var removedCount : Nat = 0;

    for (goal in allGoals.vals()) {
      let goalId = goal.id;
      let alreadySeen = Array.find<Nat>(Buffer.toArray(seenIds), func(id) = id == goalId);

      if (alreadySeen == null and goal.archived == false) {
        // First occurrence and not archived - keep it
        let (newTrie, _old) = Trie.put(
          cleanedTrie,
          keyNat(goalId),
          Nat.equal,
          goal,
        );
        cleanedTrie := newTrie;
        seenIds.add(goalId);
        Debug.print("‚úÖ Keeping goal ID=" # Nat.toText(goalId) # " name=" # goal.name);
      } else {
        // Duplicate or archived - remove it
        removedCount += 1;
        if (alreadySeen != null) {
          Debug.print("üóëÔ∏è Removing DUPLICATE goal ID=" # Nat.toText(goalId) # " name=" # goal.name);
        } else {
          Debug.print("üóëÔ∏è Removing ARCHIVED goal ID=" # Nat.toText(goalId) # " name=" # goal.name);
        };
      };
    };

    // Save cleaned trie back
    let (updatedChildToGoals, _) = Trie.put(
      childToGoals,
      keyText(childId),
      Text.equal,
      cleanedTrie,
    );
    childToGoals := updatedChildToGoals;

    let resultMessage = "Cleaned up " # Nat.toText(removedCount) # " goals. Kept " # Nat.toText(seenIds.size()) # " unique unarchived goals.";
    Debug.print("üßπ CLEANUP COMPLETE: " # resultMessage);

    return #ok(resultMessage);
  };
};
